
Document
  = Whitespace e:TermList Whitespace { return e; }



GvizShape
  = "square"
  / "rectangle"
  / "circle"



ForwardArrow "forward arrow"
  = "->"

TwoWayArrow "two way arrow"
  = "<->"

Arrow
  = ForwardArrow / TwoWayArrow



HexDigit
  = [0-9a-fA-F]

Char
  = Unescaped
  / Escape Sequence:(
        '"'
      / "\\"
      / "/"
      / "b" { return "\b"; }
      / "f" { return "\f"; }
      / "n" { return "\n"; }
      / "r" { return "\r"; }
      / "t" { return "\t"; }
      / "v" { return "\v"; }
      / "u" digits:$(HexDigit HexDigit HexDigit HexDigit) {
          return String.fromCharCode(parseInt(digits, 16));
        }
    )
    { return Sequence; }

Escape    = "\\"
QuoteMark = '"'
Unescaped = [\x20-\x21\x23-\x5B\x5D-\u10FFFF]  // explicitly omits "

ActionLabelChar
  = ActionLabelUnescaped
  / Escape Sequence:(
        '`'
      / "\\"
      / "/"
      / "b" { return "\b"; }
      / "f" { return "\f"; }
      / "n" { return "\n"; }
      / "r" { return "\r"; }
      / "t" { return "\t"; }
      / "v" { return "\v"; }
      / "u" digits:$(HexDigit HexDigit HexDigit HexDigit) {
          return String.fromCharCode(parseInt(digits, 16));
        }
    )
    { return Sequence; }

ActionLabelQuoteMark = '`'
ActionLabelUnescaped = [\x20-\x5B\x5D-\x5F\x61-\u10FFFF]  // explicitly omits `

ActionLabel "action label"
  = ActionLabelQuoteMark chars:ActionLabelChar* ActionLabelQuoteMark { return chars.join(""); }

Whitespace "whitespace"
  = [ \t\n\r\v]*

String "string"
  = QuoteMark chars:Char* QuoteMark { return chars.join(""); }

Atom "atom"
  = text:[0-9a-zA-Z\.\+\_\&\(\)\%\$\#\@\!\?\,\']+ { return text.join(''); }

Label "label"
  = atom:Atom
  / string:String



ArrowItemKey
  = "arc_label"
  / "head_label"
  / "tail_label"

ArrowItem
  = Whitespace key:ArrowItemKey Whitespace ":" Whitespace value:Label Whitespace ";" Whitespace { return {key:key, value:value}; }

ArrowItems
  = ArrowItem+

ArrowDesc
  = "{" Whitespace items:ArrowItems? Whitespace "}" { return items; }



Subexp
  = Whitespace lactl:ActionLabel?
    Whitespace ldesc:ArrowDesc?
    Whitespace arrow:Arrow
    Whitespace rdesc:ArrowDesc?
    Whitespace ractl:ActionLabel?
    Whitespace label:Label
    Whitespace tail:Subexp? {
      const base = {kind: arrow, to: label};

      if (tail && (tail !== [])) { base.se    = tail; }
      if (ldesc)                 { base.ldesc = ldesc; }
      if (rdesc)                 { base.rdesc = rdesc; }

      return base;
    }

Exp
  = label:Label se:Subexp Whitespace ';' Whitespace {
    const base = {from: label};
    if (se && (se !== [])) { base.se = se; }
    return base;
  }



ValidationKey
  = "whargarbl"
  / "todo"

ValidationItem
  = validationkey:ValidationKey ":" value:Label ";" { return {key:validationkey, value:value}; }

ValidationItems
  = ValidationItem+

ConfigValidation
  = Whitespace "validation" Whitespace ":" Whitespace "{" Whitespace validation_items:ValidationItems? Whitespace "};" Whitespace {
    return { config_kind: "validation", config_items: validation_items || [] };
  }



GvizLayout
  = "dot"
  / "circo"

StateItemShapeKey
  = "in_shape"
  / "out_shape"
  / "node_shape"

StateItemShape
  = Whitespace key:StateItemShapeKey Whitespace ":" Whitespace value:GvizShape Whitespace ";" Whitespace { return {key:key, value:value}; }

StateItemGraphLayout
  = Whitespace "graph_layout" Whitespace ":" Whitespace value:GvizLayout Whitespace ";" Whitespace { return {key:"graph_layout", value:value}; }

StateItem
  = StateItemShape
  / StateItemGraphLayout

StateItems
  = StateItem+

ConfigState
  = Whitespace "state" Whitespace ":" Whitespace "{" Whitespace state_items:StateItems? Whitespace "};" Whitespace {
    return { config_kind: "state", config_items: state_items || [] };
  }



ActionKey
  = "whargarbl"
  / "todo"

ActionItem
  = actionkey:ActionKey ":" value:Label ";" { return {key:actionkey, value:value}; }

ActionItems
  = ActionItem+

ConfigAction
  = Whitespace "action" Whitespace ":" Whitespace "{" Whitespace action_items:ActionItems? Whitespace "};" Whitespace {
    return { config_kind: "action", config_items: action_items || [] };
  }



TransitionKey
  = "whargarbl"
  / "todo"

TransitionItem
  = transitionkey:TransitionKey ":" value:Label ";" { return {key:transitionkey, value:value}; }

TransitionItems
  = TransitionItem+

ConfigTransition
  = Whitespace "transition" Whitespace ":" Whitespace "{" Whitespace transition_items:TransitionItems? Whitespace "};" Whitespace {
    return { config_kind: "transition", config_items: transition_items || [] };
  }



GraphBg
  = Whitespace "graph_bg" Whitespace ":" Whitespace value:GvizShape Whitespace ";" Whitespace { return {key:"graph_bg", value:value}; }

MinTransitionsPerState
  = Whitespace "min_transitions_per_state" Whitespace ":" Whitespace value:Label Whitespace ";" Whitespace { return {key:"min_transitions_per_state", value:value}; }

MaxTransitionsPerState
  = Whitespace "max_transitions_per_state" Whitespace ":" Whitespace value:Label Whitespace ";" Whitespace { return {key:"max_transitions_per_state", value:value}; }

GraphItem
  = GraphBg
  / MinTransitionsPerState
  / MaxTransitionsPerState

GraphItems
  = GraphItem+

ConfigGraph
  = Whitespace "graph" Whitespace ":" Whitespace "{" Whitespace graph_items:GraphItems? Whitespace "};" Whitespace {
    return { config_kind: "graph", config_items: graph_items || [] };
  }



Config
  = ConfigGraph
  / ConfigTransition
  / ConfigAction
  / ConfigState
  / ConfigValidation



Term
  = Exp
  / Config

TermList
  = term:Term*
